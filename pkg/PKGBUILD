# Arch Linux package file

# This file is part of IVRE.
# Copyright 2011 - 2017 Pierre LALET <pierre.lalet@cea.fr>
#
# IVRE is free software: you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# IVRE is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
# or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License
# along with IVRE. If not, see <http://www.gnu.org/licenses/>.

pkgname=ivre
pkgver=0.9.5.dev36
pkgrel=1
pkgdesc="Network recon framework"
url="https://ivre.rocks/"
arch=('any')
license=('GPL3')
depends=('python' 'python-crypto' 'python-pymongo')
optdepends=('python-py2neo: flow analysis support'
            'python-sqlalchemy: PostgreSQL backend support'
            'python-psycopg2: PostgreSQL backend support'
	    'python-pillow: Screenshots'
	    'python-dbus: 3D traceroute graphs'
	    'python-matplotlib: Plots')
makedepends=('git' 'python-setuptools')
source=('git+https://github.com/cea-sec/ivre.git')
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/ivre"
  python setup.py --version
}

build() {
  cd "$srcdir/ivre"
  python setup.py build
}

package() {
  cd "$srcdir/ivre"
  install -Dm644 -t "$pkgdir/usr/share/licenses/ivre/" doc/LICENSE*
  python setup.py install --root="$pkgdir" --optimize=1
}
